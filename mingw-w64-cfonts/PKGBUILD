# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=cfonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.0rust # there is also node implementation
pkgrel=1
pkgdesc='Sexy ANSI fonts for the console (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: cfonts'
)
url='https://github.com/dominikwilkowski/cfonts'
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/dominikwilkowski/cfonts/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8337423201558b43ae48e0749058a58623700cfb777288f3a520dcdb0d723a6f')

prepare() {
  cd "${_realname}-${pkgver}/rust"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  make
}

build() {
  cd "${_realname}-${pkgver}/rust"
  cargo build --locked --release
}

package() {
  cd "${_realname}-${pkgver}/rust"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm755 target/release/cfonts "${pkgdir}${MINGW_PREFIX}/bin/cfonts"
}
