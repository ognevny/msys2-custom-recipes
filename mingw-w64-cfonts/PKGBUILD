# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=cfonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.4rust # there is also node implementation
pkgrel=1
pkgdesc='Sexy ANSI fonts for the console (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: cfonts'
)
url='https://github.com/dominikwilkowski/cfonts'
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/dominikwilkowski/cfonts/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('49228dc3dd4529bd86d537f46c8aa9a47043f060ec7d050e5cb739a030222407')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target "${_target}"

  make
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"
  cargo build --locked --release
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm755 target/release/cfonts "${pkgdir}${MINGW_PREFIX}/bin/cfonts"
}
