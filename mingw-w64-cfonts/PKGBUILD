# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=cfonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.3rust # there is also node implementation
pkgrel=1
pkgdesc='Sexy ANSI fonts for the console (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: cfonts'
)
url='https://github.com/dominikwilkowski/cfonts'
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/dominikwilkowski/cfonts/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        # fix lockfile
        "https://github.com/dominikwilkowski/cfonts/commit/19fd40fdea06ad0285b6a7d96c86f4eff62a40e7.patch")
sha256sums=('e5b2be2d1ce4f4e5b345d755425bc9841a9c1e35c4025ec83dab4f6c38291d87'
            '3a27a660e0addf39e5f5566ec1c3df46bd129c476f73ca959a4693367c43fc46')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"

  patch -Np2 -i "${srcdir}/19fd40fdea06ad0285b6a7d96c86f4eff62a40e7.patch"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target "${_target}"

  make
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"
  cargo build --locked --release
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/rust"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm755 target/release/cfonts "${pkgdir}${MINGW_PREFIX}/bin/cfonts"
}
