# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=fkysoxide
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.0
pkgrel=1
pkgdesc="A FKYS lang interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/ognevnydemon/fkys-rs"
license=('spdx:WTFPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('b8a1badd326b16b581d2ae0e0aa8be13bba08e61f7bacb1efe9bc91659ca3c88')

prepare() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi

  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo build --release --locked
}

package() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
