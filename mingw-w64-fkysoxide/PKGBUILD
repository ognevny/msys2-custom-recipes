# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=fkysoxide
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.6
pkgrel=1
pkgdesc="A FKYS lang interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/ognevnydemon/fkys-rs"
license=('spdx:WTFPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('!strip' '!lto') # they're enabled in Cargo manifest
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f2eabf646c57cbae6ed06ab47f40abbb64376cda9f197fc0ef40111513c631c4')

prepare() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi

  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo build --release --locked
}

check() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo test --release --test '*'
}

package() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
