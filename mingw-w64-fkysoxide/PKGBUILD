# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=fkysoxide
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.1
pkgrel=1
pkgdesc="A FKYS lang interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/ognevnydemon/fkys-rs"
license=('spdx:WTFPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('abbddc516ff397730832fa13c93490720a2df044f9d894191d5191f7b6fa61b6')

prepare() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi

  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo build --release --locked
}

package() {
  cd "${srcdir}/fkys-rs-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
