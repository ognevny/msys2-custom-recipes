# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=hello-world.rs
_commit=6ba6791c8652ae213fc3f0ed079944fbe6bfb049
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=0.1.0.r359.6ba6791
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mTvare6/hello-world.rs"
pkgdesc="Memory safe, blazing fast, configurable, minimal hello world written in rust under 1 line of code with few dependencies (mingw-w64)"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-lmdb"
             'git')
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-atkmm"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2")
source=("${_realname}::git+https://github.com/ognevnydemon/hello-world.rs-mingw#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  echo "$(awk '/^VERSION =/ {print $3}' config.mk)".r"$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  cargo vendor \
    --locked \
    --versioned-dirs

  patch -Np0 -i fix-deps.patch
}

build() {
  cd "${srcdir}/${_realname}"
  export PKG_CONFIG_PATH="${MINGW_PREFIX}/lib/pkgconfig"
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  make
}

package() {
  cd "${srcdir}/${_realname}"
  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.rs "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
}
