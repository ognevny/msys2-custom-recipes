# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rustpython
_commit=940b879950171a70a20de55d5745920af79cab27
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r13661.940b87995
pkgrel=1
pkgdesc="A Python Interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/RustPython/RustPython"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-sqlite3"
             'git')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-bzip2")
source=("${_realname}::git+${url}#commit=${_commit}"
        "fix-pylib-path.patch"
        "use-system-libs.patch")
sha256sums=('SKIP'
            'd114917f3eaeccaa63840f27aa086a7265c3a535f68d8f2e2409c18c796fe049'
            '7f792966bf803cd602c15434d58a78bf5bea509cd66a572a01dfcbabc1cabe75')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_env() {
  export LIBZ_SYS_STATIC=0
  export OPENSSL_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -Np1 -i "${srcdir}/fix-pylib-path.patch"

  cargo vendor --locked --versioned-dirs
  cat >> .cargo/config.toml <<END

[source.crates-io]
replace-with = "vendored-sources"

[source.vendored-sources]
directory = "vendor"
END

  patch -Np1 -i "${srcdir}/use-system-libs.patch"
}

build() {
  cd "${srcdir}/${_realname}"

  _env
  cargo build --release --locked --features "bz2 jit ssl freeze-stdlib"
}

check() {
  cd "${srcdir}/${_realname}"

  _env
  cargo test --release --locked --features "bz2 jit ssl freeze-stdlib"
}

package() {
  cd "${srcdir}/${_realname}"

  _env
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --features "bz2 jit ssl freeze-stdlib" \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
