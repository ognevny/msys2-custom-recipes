# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rustpython
_commit=1c93c1630b536d398ab86f91b0f3424aad76ddc8
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r13468.1c93c1630
pkgrel=1
pkgdesc="A Python Interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/RustPython/RustPython"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             'git')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libffi")
source=("${_realname}::git+${url}#commit=${_commit}"
        "fix-pylib-path.patch"
        "use-system-libffi.patch")
sha256sums=('SKIP'
            'd114917f3eaeccaa63840f27aa086a7265c3a535f68d8f2e2409c18c796fe049'
            '54d3c2790d366b02591efdeda8b0c996c794d8f7d50fc04c2c2581571c24c1d4')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"

  patch -Np1 -i "${srcdir}/fix-pylib-path.patch"
  patch -Np1 -i "${srcdir}/use-system-libffi.patch"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi

  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${srcdir}/${_realname}"

  LIBZ_SYS_STATIC=0 \
  OPENSSL_NO_VENDOR=1 \
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 \
    cargo build --release --locked --features "bz2 jit ssl freeze-stdlib"
}

check() {
  cd "${srcdir}/${_realname}"

  LIBZ_SYS_STATIC=0 \
  OPENSSL_NO_VENDOR=1 \
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 \
    cargo test --release --locked --features "bz2 jit ssl freeze-stdlib"
}

package() {
  cd "${srcdir}/${_realname}"

  LIBZ_SYS_STATIC=0 \
  OPENSSL_NO_VENDOR=1 \
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 \
    cargo install \
      --frozen \
      --offline \
      --no-track \
      --features "bz2 jit ssl freeze-stdlib" \
      --path . \
      --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
