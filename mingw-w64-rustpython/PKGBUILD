# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rustpython
_commit=a620b38ba0781ea20d2d9bfbb1c432de7e713982
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r13808.a620b38ba
pkgrel=1
pkgdesc="A Python Interpreter written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/RustPython/RustPython"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-bzip2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-sqlite3"
             'git')
source=("${_realname}::git+${url}#commit=${_commit}"
        "bzip2-sys.tar.gz::https://crates.io/api/v1/crates/bzip2-sys/0.1.11+1.0.8/download"
        "libz-sys.tar.gz::https://crates.io/api/v1/crates/libz-sys/1.1.8/download"
        "link-dynamic-bzip.patch"
        "link-dynamic-zlib.patch"
        "fix-pylib-path.patch")
sha256sums=('059abee548f828f563805d552cb272f45da19ce0f137986174ee6c1a46fc5c99'
            '736a955f3fa7875102d57c82b8cac37ec45224a07fd32d58f9f7a186b6cd4cdc'
            '9702761c3935f8cc2f101793272e202c72b99da8f4224a19ddcf1279a6450bbf'
            '35db3a89853afa5676cdff9374b248b7b4df6eebb2936f40f8d585d68ea5de2d'
            '50dce4223eba7a5c6ba6c51d8ccfd45039799a3b4408827a04349d03089b8817'
            'd114917f3eaeccaa63840f27aa086a7265c3a535f68d8f2e2409c18c796fe049')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_env() {
  export OPENSSL_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -Np1 -i "${srcdir}/fix-pylib-path.patch"

  sed -i '/\[patch\.crates-io\]/a bzip2-sys = { path = "../bzip2-sys-0.1.11+1.0.8" }\nlibz-sys = { path = "../libz-sys-1.1.8" }' Cargo.toml
  patch -d "../bzip2-sys-0.1.11+1.0.8" -i "${srcdir}/link-dynamic-bzip.patch"
  patch -d "../libz-sys-1.1.8" -i "${srcdir}/link-dynamic-zlib.patch"
  sed -i 's/^libffi = "3.1.0"$/libffi = { version = "3.1.0", features = ["system"] }/' jit/Cargo.toml

  cargo update -p bzip2-sys -p libz-sys
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}"

  _env
  cargo build --release --locked --features "bz2 jit ssl freeze-stdlib"
}

check() {
  cd "${srcdir}/${_realname}"

  _env
  cargo test --release --locked --features "bz2 jit ssl freeze-stdlib"
}

package() {
  cd "${srcdir}/${_realname}"

  _env
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --features "bz2 jit ssl freeze-stdlib" \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
